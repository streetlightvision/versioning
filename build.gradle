/**
 * Plug-in development
 */

plugins {
    id 'nu.studer.plugindev' version '1.0.3'
    id 'com.jfrog.bintray' version '1.3.1'
    /**
     * Versioning applied to itself
     */
    id 'net.nemerosa.versioning' version '1.7.1'
}

apply plugin: 'groovy'

/**
 * Meta information
 */

group = 'net.nemerosa'
version = versioning.info.display

/**
 * Dependencies
 */

dependencies {
    localGroovy()
    compile 'org.ajoberstar:grgit:1.6.0'
    compile 'org.tmatesoft.svnkit:svnkit:1.8.12'
}

/**
 * Plug-in definition
 */

plugindev {
    pluginImplementationClass 'net.nemerosa.versioning.VersioningPlugin'
    pluginDescription 'Gradle plug-in that computes version information from the SCM'
    pluginLicenses 'MIT'
    pluginTags 'gradle', 'plugin', 'scm', 'git', 'svn', 'version'
    authorId 'dcoraboeuf'
    authorName 'Damien Coraboeuf'
    authorEmail 'damien.coraboeuf@gmail.com'
    projectUrl 'https://github.com/nemerosa/versioning'
    projectInceptionYear '2015'
    done()
}

/**
 * Plug-in publication
 */

bintray {
    user = bintrayUser
    key = bintrayApiKey
    pkg {
        repo = 'nemerosa'
        version {
            attributes = [
                    'gradle-plugin': 'net.nemerosa.versioning:net.nemerosa:versioning'
            ]
        }
    }
}

/**
 * Acceptance tests
 */

apply from: 'gradle/acceptance.gradle'

/**
 * Wrapper definition
 */

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
